buildscript {
    ext.kotlin_version = '1.2.71'

    ext.pluginname = 'Kotlin4MC'
    ext.plugindesc = 'Kotlin for Minecraft'

    repositories {
        mavenCentral()
    }
    dependencies{
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'fr.rhaz.minecraft'
version '2.0.1'
def pname = "Kotlin4MC"

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    jcenter()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"}
    maven { url 'https://repo.spongepowered.org/maven'}
    maven { url 'https://dl.bintray.com/kotlin/exposed/'}
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.25.0'
    compileOnly "net.md-5:bungeecord-api:1.12-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    compileOnly "org.spongepowered:spongeapi:7.0.0"
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/jar")
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

sourceSets {
    main.kotlin.srcDirs += 'src'
    test.kotlin.srcDirs += 'test'
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    with copySpec {
        from 'resources'
        filter(ReplaceTokens, tokens: ['id':rootProject.name, 'name':pluginname, 'version':version, 'desc':plugindesc])
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    repositories {
        maven {
            url System.getProperties().getProperty("mavenwrite")
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

